plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.lodz.android.conjurerdemo"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"
    }

    viewBinding {
        enabled = true
    }

    def FILE_AUTHORITY = "FILE_AUTHORITY"
    defaultConfig {
        buildConfigField "String", FILE_AUTHORITY, "\"com.lodz.android.conjurerdemo.fileprovider\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildTypes {
        debug {//debug版本的配置
            minifyEnabled false // 是否进行混淆
            zipAlignEnabled false // 是否支持zip
            debuggable true // 是否支持调试
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [FILE_AUTHORITY: defaultConfig.getBuildConfigFields().get("FILE_AUTHORITY").value.replace("\"", "")]
        }

        release {
            minifyEnabled false // 是否进行混淆
            zipAlignEnabled true // 是否支持zip
            debuggable false // 是否支持调试
            jniDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [FILE_AUTHORITY: defaultConfig.getBuildConfigFields().get("FILE_AUTHORITY").value.replace("\"", "")]
        }
    }
    
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    namespace 'com.lodz.android.conjurerdemo'

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def newName
            if (variant.baseName.endsWith('release')) {
                def formattedDate = new Date().format('yyyyMMddHHmm')
                newName = "Conjurer-v" + defaultConfig.versionName + "-" + variant.baseName + "-" + formattedDate + ".apk"
            } else {
                newName = "Conjurer-v" + defaultConfig.versionName + "-" + variant.baseName + ".apk"
            }
            outputFileName = newName
        }
    }
}

dependencies {
    implementation 'ink.lodz:pandora:2.0.3'
    implementation 'ink.lodz:imageloader-kt:1.2.3'
    // 权限申请
    implementation "com.github.permissions-dispatcher:ktx:1.1.4"
    //谷歌电话号码归属地库
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.12.54'
    implementation 'com.googlecode.libphonenumber:carrier:1.99'
    implementation 'com.googlecode.libphonenumber:geocoder:2.99'
    implementation 'com.googlecode.libphonenumber:prefixmapper:2.190'

    implementation project(path: ':conjurer')
}